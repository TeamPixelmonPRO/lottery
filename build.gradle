plugins {
    id "com.zoltu.kotlin" version "1.0.6"
    id "com.qixalite.spongestart" version "1.5.2"
    id "com.github.johnrengelman.shadow" version "1.2.4"
    id "flavor.pie.promptsign" version "1.0.2"
}

group "de.randombyte"
version "1.3.1"

repositories {
    jcenter()
    maven { url "https://repo.spongepowered.org/maven/" }
    maven { url "https://jitpack.io" }
}

spongestart {
    eula true
    spongeVanillaBuild = "1.10.2-5.1.0-BETA-366"
}

ext.spongeApiVersion = "5.0.0"
ext.kotlinVersion = "1.0.6"

configurations {
    compile.extendsFrom shadow
}

dependencies {
    shadow "org.jetbrains.kotlin:kotlin-runtime:$kotlinVersion"
    shadow "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "org.spongepowered:spongeapi:$spongeApiVersion"
    kapt "org.spongepowered:spongeapi:$spongeApiVersion"
    shadow("com.github.randombyte-developer.kosp:kosp:v0.3.10.1") {
        transitive = false
    }
}

jar.enabled = false

shadowJar {
    configurations = [project.configurations.shadow]

    exclude "StartServer*.*"

    relocate "kotlin", "de.randombyte.lottery.internal.kotlin"
    relocate "de.randombyte.kosp", "de.randombyte.lottery.internal.kosp"

    classifier = null // Remove '-all' suffix from output file name
}
build.dependsOn shadowJar
signArchives.dependsOn shadowJar